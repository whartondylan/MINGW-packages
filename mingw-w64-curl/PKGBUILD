# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=curl
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=7.87.0
pkgrel=3
pkgdesc="Command line tool and library for transferring data with URLs. (mingw-w64)"
arch=('any')
url="https://curl.haxx.se"
license=("MIT")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc" "${MINGW_PACKAGE_PREFIX}-pkg-config")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-c-ares"
         "${MINGW_PACKAGE_PREFIX}-libidn2"
         "${MINGW_PACKAGE_PREFIX}-libpsl"
         "${MINGW_PACKAGE_PREFIX}-libssh2"
         "${MINGW_PACKAGE_PREFIX}-zlib"
         "${MINGW_PACKAGE_PREFIX}-ca-certificates"
         "${MINGW_PACKAGE_PREFIX}-openssl"
         "${MINGW_PACKAGE_PREFIX}-nghttp2"
         "${MINGW_PACKAGE_PREFIX}-brotli"
         "${MINGW_PACKAGE_PREFIX}-zstd")
options=('staticlibs')
source=("${url}/download/${_realname}-${pkgver}.tar.bz2"{,.asc}
        "0001-Make-cURL-relocatable.patch"
        "0002-Hack-make-relocation-work-inside-libexec-git-core-an.patch"
        https://github.com/curl/curl/commit/54120efdf64c40c93308dee3935bbed7e1cdc1aa.patch)
sha256sums=('5d6e128761b7110946d1276aff6f0f266f2b726f5e619f7e0a057a474155f307'
            'SKIP'
            'd8386671a7819d39bb71209f2cd255bc91c803494f26639aed6e62f42694faec'
            'e3387326ca48c740309171063274408febb9ee0ec5de601ae77ac77b8e490999'
            'd142acb6a638995a9cc418fc22c947f29d9103da9861266c7022e9b1dea093cd')
validpgpkeys=('914C533DF9B2ADA2204F586D78E11C6B279D5C91'  # Daniel Stenberg
              '27EDEAF22F3ABCEB50DB9A125CC908FDB71E12C2'
              '4461EAF0F8E9097F48AF0555F9FEAFF9D34A1BDB')

if [[ "$MSYSTEM" == CLANG* && -z "$WITHOUT_PDBS" ]]
then
	pkgname+=("${MINGW_PACKAGE_PREFIX}-${_realname}-pdb")
	options+=('!strip')
	STRIP=llvm-strip
	STRIP_OPTS=--strip-debug
elif test -z "$WITHOUT_PDBS"
then
	pkgname+=("${MINGW_PACKAGE_PREFIX}-${_realname}-pdb")
	makedepends+=("${MINGW_PACKAGE_PREFIX}-cv2pdb")
	options+=('!strip')
	STRIP=cv2pdb
else
	options+=('strip')
fi

prepare() {
  cd "${_realname}-${pkgver}"
  # MINGW builds typically mean that prepare is run twice: once for 64-bit and
  # once for 64-bit. These files are therefore added twice by below patches, so
  # let's force-delete them before trying to add them.
  rm -f lib/pathtools.h lib/pathtools.c tests/data/test2070 >/dev/null 2>&1 || true
  patch -p1 -i "${srcdir}/0001-Make-cURL-relocatable.patch"
  patch -p1 -i "${srcdir}/0002-Hack-make-relocation-work-inside-libexec-git-core-an.patch"
  patch -p1 -i "${srcdir}/54120efdf64c40c93308dee3935bbed7e1cdc1aa.patch"
  autoreconf -vfi
}

build() {
  [[ "$MSYSTEM" == CLANG* ]] && export LDFLAGS="-Wl,-pdb=" # Ensures that Clang generates PDB files

  local -a extra_config
  extra_config+=( --disable-debug )
  mkdir -p "${srcdir}/build-${CARCH}"
  extra_config+=("--with-ssl")
  extra_config+=("--with-ca-bundle=${MINGW_PREFIX}/ssl/certs/ca-bundle.crt")
  extra_config+=('--with-nghttp2=${MINGW_PREFIX}/')
  extra_config+=("--with-schannel")
  cd "${srcdir}/build-${CARCH}"
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --without-random \
    --without-librtmp \
    --enable-static \
    --enable-shared \
    --enable-sspi \
    --with-libssh2 \
    "${extra_config[@]}"
# there's a bug with zsh completion generation script and Windows.
# curl has to specified with the file extension.
  sed -i "s|\/curl > \$\@|\/curl\$\{EXEEXT\} > \$\@|" scripts/Makefile
  make
  case "${pkgname[@]}"
  in *pdb*)
    $STRIP $STRIP_OPTS lib/.libs/libcurl-4.dll
    $STRIP $STRIP_OPTS src/.libs/curl.exe
  esac
}

package_1() {
  cd "${srcdir}/build-${CARCH}"

  test pdb != "$1" || {
    install -d -m 755 "${pkgdir}${MINGW_PREFIX}/bin"
    install -m 755 lib/.libs/libcurl-4.pdb "${pkgdir}${MINGW_PREFIX}/bin"
    install -m 755 src/.libs/curl.pdb "${pkgdir}${MINGW_PREFIX}/bin"
    return
  }

  make DESTDIR="${pkgdir}" install

  local PREFIX_DEPS=$(cygpath -am ${MINGW_PREFIX})
  sed -s "s|${PREFIX_DEPS}|${MINGW_PREFIX}|g" -i ${pkgdir}${MINGW_PREFIX}/bin/curl-config
}

package_mingw-w64-i686-curl() {
  package_1
}

package_mingw-w64-x86_64-curl() {
  package_1
}

package_mingw-w64-clang-aarch64-curl() {
  package_1
}

package_mingw-w64-i686-curl-pdb() {
  package_1 pdb
}

package_mingw-w64-x86_64-curl-pdb() {
  package_1 pdb
}

package_mingw-w64-clang-aarch64-curl-pdb() {
  package_1 pdb
}

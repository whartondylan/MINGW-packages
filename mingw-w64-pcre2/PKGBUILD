# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=pcre2
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=10.46
pkgrel=1
pkgdesc="A library that implements Perl 5-style regular expressions (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url="https://pcre.org/"
license=("BSD")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc" "${MINGW_PACKAGE_PREFIX}-autotools")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-bzip2"
         #"${MINGW_PACKAGE_PREFIX}-readline"
         "${MINGW_PACKAGE_PREFIX}-wineditline"
         "${MINGW_PACKAGE_PREFIX}-zlib")
options=('staticlibs' 'strip')
source=(https://github.com/PhilipHazel/pcre2/releases/download/${_realname}-${pkgver}/${_realname}-${pkgver}.tar.bz2{,.sig}
        0001-RunTest-skip-a-couple-more-tests-on-Windows.patch
        0002-RunGrepTest-compare-without-paying-attention-to-line.patch
        0003-Fix-INTERACTIVE-fd-on-Windows.patch
        0004-pcre2grep-avoid-letting-Windows-handle-command-line-.patch
        0005-RunGrepTest-work-around-Unix-vs-Windows-path-issues.patch
        0006-RunGrepTest-Windows-does-not-have-a-bin-echo.patch
        0007-RunGrepTest-work-around-MSYS2-idiosyncrasy.patch)
sha256sums=('15fbc5aba6beee0b17aecb04602ae39432393aba1ebd8e39b7cabf7db883299f'
            'SKIP'
            'd3c8b81d51b3de2fa60d62625771bcec3ab7f4c2365a400ab4c94fcbafe81de9'
            '1746e53b28c6e670c4375dc325a7707f65c7b9e37af870949886cab5723855ef'
            '4d672ef554d1c371738d2afb6cde4b8d280d6ce8cf110c3f81a2de77d1462c44'
            'c17b7b63f218083b680214b3f767efbe9d6543d85438136fc4aa4a0f212ace9c'
            'a5722abc0cf3cbdacaffb0e8f78bae3f8821908456079c76131511feacd35661'
            'c6dadfe24bcd669ad56e2a47d45a62bc9c1affe4319c45bc9e30efdf77a1c15b'
            '47db0aa514176f679e692b780753f473e9c2b66a737fdf635e03b6ec0f9f00d6')
validpgpkeys=('A95536204A3BB489715231282A98E77EB6F24CA8') # Nicholas Wilson <nicholas@nicholaswilson.me.uk>

prepare () {
  cd "${srcdir}/${_realname}-${pkgver}"

  patch -p1 <"${srcdir}"/0001-RunTest-skip-a-couple-more-tests-on-Windows.patch
  patch -p1 <"${srcdir}"/0002-RunGrepTest-compare-without-paying-attention-to-line.patch
  patch -p1 <"${srcdir}"/0003-Fix-INTERACTIVE-fd-on-Windows.patch
  patch -p1 <"${srcdir}"/0004-pcre2grep-avoid-letting-Windows-handle-command-line-.patch
  patch -p1 <"${srcdir}"/0005-RunGrepTest-work-around-Unix-vs-Windows-path-issues.patch
  patch -p1 <"${srcdir}"/0006-RunGrepTest-Windows-does-not-have-a-bin-echo.patch
  patch -p1 <"${srcdir}"/0007-RunGrepTest-work-around-MSYS2-idiosyncrasy.patch
}

build() {
  [[ -d ${srcdir}/build-${MSYSTEM} ]] && rm -rf ${srcdir}/build-${MSYSTEM}
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  "${srcdir}"/${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --enable-jit \
    --enable-pcre2-8 \
    --enable-pcre2-16 \
    --enable-pcre2-32 \
    --enable-pcre2grep-libz \
    --enable-pcre2grep-libbz2 \
    --enable-pcre2test-libedit

  make
}

check() {
  cd "${srcdir}/build-${MSYSTEM}"

  # Work around bug in PCRE2 10.44
  test i686 != "$CARCH" ||
  for f in testdata/testoutput8-8-2 testdata/testoutput8-16-2 testdata/testoutput8-32-2
  do
    cp "../${_realname}-${pkgver}/$f" "../${_realname}-${pkgver}/$f.backup"
    perl -pi -e 's{^(Memory allocation - compiled block : )(\d+)$}{$1 . ($2 - 24)}e' "../${_realname}-${pkgver}/$f"
  done

  make check || return 1

  for f in testdata/testoutput8-8-2 testdata/testoutput8-16-2 testdata/testoutput8-32-2
  do
    test ! -f "../${_realname}-${pkgver}/$f.backup" ||
    mv "../${_realname}-${pkgver}/$f.backup" "../${_realname}-${pkgver}/$f"
  done
}

package() {
  cd "${srcdir}/build-${MSYSTEM}"
  make DESTDIR="${pkgdir}" install

  # License files
  cd "${pkgdir}${MINGW_PREFIX}/share"
  mkdir -p licenses/${_realname}
  mv doc/${_realname}/COPYING licenses/${_realname}
  mv doc/${_realname}/LICENCE.md licenses/${_realname}
}
